import aQute.bnd.gradle.Bundle

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.hidetake:gradle-ssh-plugin:2.10.1'
        classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:4.2.0'
    }
}

plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.10.2'
    id "com.jfrog.bintray" version "1.8.4"
}

apply plugin: 'groovy'
apply plugin: 'biz.aQute.bnd.builder'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'signing'
apply plugin: 'pl.allegro.tech.build.axion-release'
apply from: 'gradle/dist.gradle'

group = 'org.mnode.ical4j'
description = '''\
MS Outlook-compatible Timezone Definitions
'''

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
    // sonatype snapshots
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    compile 'org.mnode.ical4j:ical4j:2.+'

    testCompile 'junit:junit:4.11'
}

task bundle(type: Bundle) {
    from(project.rootDir) {
        include "LICENSE.txt"
    }
    from sourceSets.main.output
    bndfile = 'bnd.bnd'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

jar.enabled = false

artifacts {
    archives bundle
    archives javadocJar
    archives sourcesJar
}

scmVersion {
    tag {
        prefix = 'ical4j-zoneinfo-outlook'
    }
    versionCreator 'versionWithBranch'
    branchVersionCreator = [
        'master': 'simple'
    ]
}

version = scmVersion.version

ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatype_username, password: sonatype_password)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: sonatype_username, password: sonatype_password)
            }

            pom.project {
                name project.name
                packaging 'jar'
                description project.description
                url 'http://ical4j.github.io'

                scm {
                    url 'https://github.com/ical4j/ical4j-zoneinfo-outlook'
                    connection 'scm:git@github.com:ical4j/ical4j-zoneinfo-outlook.git'
                    developerConnection 'scm:git@github.com:ical4j/ical4j-zoneinfo-outlook.git'
                }

                licenses {
                    license {
                        name 'iCal4j - License'
                        url 'https://raw.githubusercontent.com/ical4j/ical4j/master/LICENSE'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'fortuna'
                        name 'Ben Fortuna'
                    }
                }
            }
        }
    }
}

publishing {
    publications {
        mavenArtifacts(MavenPublication) {
            from components.java
            artifact javadocJar
            artifact sourcesJar
            pom.withXml {
                asNode().appendNode('name', project.name)
                asNode().appendNode('description', project.description)
                asNode().appendNode('url', 'http://ical4j.github.io')

                def scmNode = asNode().appendNode('scm')
                scmNode.appendNode('url', 'https://github.com/ical4j/ical4j-zoneinfo-outlook')
                scmNode.appendNode('connection', 'scm:git@github.com:ical4j/ical4j-zoneinfo-outlook.git')
                scmNode.appendNode('developerConnection', 'scm:git@github.com:ical4j/ical4j-zoneinfo-outlook.git')

                def licenseNode = asNode().appendNode('licenses').appendNode('license')
                licenseNode.appendNode('name', 'iCal4j - License')
                licenseNode.appendNode('url', 'https://raw.githubusercontent.com/ical4j/ical4j-zoneinfo-outlook/master/LICENSE.txt')
                licenseNode.appendNode('distribution', 'repo')

                def developerNode = asNode().appendNode('developers').appendNode('developer')
                developerNode.appendNode('id', 'fortuna')
                developerNode.appendNode('name', 'Ben Fortuna')
            }
        }
    }
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
//    sign configurations.archives
    sign publishing.publications.mavenArtifacts
}

bintray {
    user = bintray_user
    key = bintray_key
    pkg {
        repo = 'maven'
        name = 'ical4j-zoneinfo-outlook'
        userOrg = 'ical4j'
        licenses = ['BSD']
        vcsUrl = 'https://github.com/ical4j/ical4j-zoneinfo-outlook.git'
        version {
            name = scmVersion.version
            desc = "iCal4j ZoneInfo Outlook $scmVersion.version"
            released  = new Date()
            vcsTag = "ical4j-zoneinfo-outlook-$scmVersion.version"
            gpg {
                sign = isReleaseVersion
            }
            mavenCentralSync {
                sync = isReleaseVersion
                user = sonatype_username //OSS user token: mandatory
                password = sonatype_password //OSS user password: mandatory
                close = '0' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
    }
//    configurations = ['archives']
    publications = ['mavenArtifacts']
}
