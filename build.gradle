plugins {
    id 'java'
    id 'groovy'
    id "biz.aQute.bnd.builder" version "5.1.2"
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'pl.allegro.tech.build.axion-release' version '1.13.1'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    api "org.mnode.ical4j:ical4j:$ical4jVersion"


    testCompile "org.codehaus.groovy:groovy:$groovyVersion"

    // junit
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.9.0'

    testImplementation platform("org.spockframework:spock-bom:2.1-groovy-3.0"),
        "org.spockframework:spock-core",
        "org.slf4j:slf4j-log4j12:$slf4jVersion"
}

test {
    useJUnitPlatform()
}

javadoc {
    options {
        links 'https://docs.oracle.com/en/java/javase/11/docs/api/',
            'http://ical4j.github.io/docs/ical4j/api/4.0.0-alpha6/'
    }
}

scmVersion {
    tag {
        prefix = 'ical4j-zoneinfo-outlook-'
    }
    versionCreator 'versionWithBranch'
    branchVersionCreator = [
            'master': 'simple'
    ]
}

group = 'org.mnode.ical4j'
description = 'Alternate timezone definitions for greater compatbility with Outlook'
version = scmVersion.version

jar {
    manifest {
        attributes (
                'Implementation-Title': 'iCal4j ZoneInfo Outlook',
                'Implementation-Version': version,
                'Implementation-Vendor': 'Ben Fortuna'
        )
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

publishing {
    publications {
        mavenArtifacts(MavenPublication) {
            from components.java
            artifact javadocJar
            artifact sourcesJar
            pom.withXml {
                asNode().appendNode('name', project.name)
                asNode().appendNode('description', project.description)
                asNode().appendNode('url', 'http://ical4j.github.io')

                def scmNode = asNode().appendNode('scm')
                scmNode.appendNode('url', 'https://github.com/ical4j/ical4j-zoneinfo-outlook')
                scmNode.appendNode('connection', 'scm:git@github.com:ical4j/ical4j-zoneinfo-outlook.git')
                scmNode.appendNode('developerConnection', 'scm:git@github.com:ical4j/ical4j-zoneinfo-outlook.git')

                def licenseNode = asNode().appendNode('licenses').appendNode('license')
                licenseNode.appendNode('name', 'iCal4j - License')
                licenseNode.appendNode('url', 'https://raw.githubusercontent.com/ical4j/ical4j-zoneinfo-outlook/master/LICENSE.txt')
                licenseNode.appendNode('distribution', 'repo')

                def developerNode = asNode().appendNode('developers').appendNode('developer')
                developerNode.appendNode('id', 'fortuna')
                developerNode.appendNode('name', 'Ben Fortuna')
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = version.endsWith('SNAPSHOT') ? "https://oss.sonatype.org/content/repositories/snapshots/" : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

signing {
    required { isReleaseVersion }
    sign publishing.publications.mavenArtifacts
}
