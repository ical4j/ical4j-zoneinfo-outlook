buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'http://maven.tmatesoft.com/content/repositories/releases/'
        }
    }

    dependencies {
        classpath('au.com.ish.gradle:release:2.2.2') {
            exclude module: 'svnkit'
        }
    }
}

apply plugin: 'groovy'
apply plugin: 'osgi'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'release'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.mnode.ical4j:ical4j:1.0.7'

    testCompile 'junit:junit:4.11'
}

ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")

    sonatypeUsername = project.hasProperty('sonatypeUsername') ? project.getProperty('sonatypeUsername') : ''
    sonatypePassword = project.hasProperty('sonatypePassword') ? project.getProperty('sonatypePassword') : ''
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    //required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    required { isReleaseVersion }
    sign configurations.archives
}

release {
    failOnSnapshotDependencies = true
    // allow modifications due to bug in the release plugin..
    allowLocalModifications = true
    scm = 'git'
}

group = 'org.mnode.ical4j'
version = System.getenv('RELEASE_VERSION') ?: release.projectVersion
description = '''
MS Outlook-compatible Timezone Definitions
'''

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'iCal4j ZoneInfo Outlook'
                packaging 'jar'
                description project.description
                url 'http://ical4j.github.io'

                scm {
                    url 'https://github.com/ical4j/ical4j-zoneinfo-outlook'
                    connection 'scm:git@github.com:ical4j/ical4j-zoneinfo-outlook.git'
                    developerConnection 'scm:git@github.com:ical4j/ical4j-zoneinfo-outlook.git'
                }

                licenses {
                    license {
                        name 'iCal4j - License'
                        url 'https://raw.githubusercontent.com/ical4j/ical4j/master/LICENSE'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'fortuna'
                        name 'Ben Fortuna'
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}
